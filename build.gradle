subprojects {
  apply plugin: "java-library"
  apply plugin: "maven-publish"

  java {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    // Forcing Spring to use logback instead of commons-logging
    testImplementation "ch.qos.logback:logback-classic:1.3.14" // Not using 1.4.x yet as it requires Java 11
    testImplementation 'org.slf4j:jcl-over-slf4j:2.0.17'
    testImplementation 'org.slf4j:slf4j-api:2.0.17'

    // Automatic loading of test framework implementation dependencies is deprecated.
    // https://docs.gradle.org/current/userguide/upgrading_version_8.html#test_framework_implementation_dependencies
    // Without this, once using JUnit 5.12 or higher, Gradle will not find any tests and report an error of:
    // org.junit.platform.commons.JUnitException: TestEngine with ID 'junit-jupiter' failed to discover tests
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.13.4"
  }

  javadoc.failOnError = false
  // Ignores warnings on params that don't have descriptions, which is a little too noisy
  javadoc.options.addStringOption('Xdoclint:none', '-quiet')
}

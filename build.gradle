subprojects {
  apply plugin: "java-library"
  apply plugin: "maven-publish"

  group = "com.marklogic"
  version = "1.5-SNAPSHOT"

  java {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }

  repositories {
    mavenCentral()

    // For local development
    mavenLocal()

    maven {
      url "https://bed-artifactory.bedford.progress.com:443/artifactory/ml-maven-snapshots/"
    }
  }

  dependencies {
    // Forcing Spring to use logback instead of commons-logging
    testImplementation "ch.qos.logback:logback-classic:1.3.14" // Not using 1.4.x yet as it requires Java 11
    testImplementation 'org.slf4j:jcl-over-slf4j:2.0.16'
    testImplementation 'org.slf4j:slf4j-api:2.0.16'
  }

  javadoc.failOnError = false
  // Ignores warnings on params that don't have descriptions, which is a little too noisy
  javadoc.options.addStringOption('Xdoclint:none', '-quiet')

  publishing {
    repositories {
      maven {
        if (project.hasProperty("mavenUser")) {
          credentials {
            username mavenUser
            password mavenPassword
          }
          url publishUrl
          allowInsecureProtocol = true
        } else {
          name = "central"
          url = mavenCentralUrl
          credentials {
            username mavenCentralUsername
            password mavenCentralPassword
          }
        }
      }
    }
  }
}

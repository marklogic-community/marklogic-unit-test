plugins {
	// ml-gradle is used for deploying a test application so that this project itself can be tested
	id "com.marklogic.ml-gradle" version "5.0.0"
}

dependencies {
	api "com.marklogic:marklogic-client-api:${javaClientVersion}"
  implementation "org.slf4j:slf4j-api:2.0.17"

  testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
}

test {
  useJUnitPlatform()
}

// Publishing setup - see https://docs.gradle.org/current/userguide/publishing_setup.html .
java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mainJava(MavenPublication) {
      groupId = group
      version = version
      from components.java
      pom {
        name = "${group}:${project.name}"
        description = "Supports testing MarkLogic applications"
        packaging = "jar"
        url = "https://github.com/marklogic-community/${project.name}"
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            id = "marklogic"
            name = "MarkLogic Github Contributors"
            email = "general@developer.marklogic.com"
            organization = "MarkLogic"
            organizationUrl = "https://www.marklogic.com"
          }
        }
        scm {
          url = "git@github.com:marklogic-community/${project.name}.git"
          connection = "scm:git@github.com:marklogic-community/${project.name}.git"
          developerConnection = "scm:git@github.com:marklogic-community/${project.name}.git"
        }
      }
    }
  }
  repositories {
    maven {
      if (project.hasProperty("mavenUser")) {
        credentials {
          username mavenUser
          password mavenPassword
        }
        url publishUrl
        allowInsecureProtocol = true
      } else {
        name = "central"
        url = mavenCentralUrl
        credentials {
          username mavenCentralUsername
          password mavenCentralPassword
        }
      }
    }
  }
}

task createHttpCredentials(type: com.marklogic.gradle.task.MarkLogicTask) {
  doLast {
    def client = getAppConfig().newAppServicesDatabaseClient("Security")
    String xquery = """
xquery version "1.0-ml";

import module namespace sec = "http://marklogic.com/xdmp/security"
      at "/MarkLogic/security.xqy";

declare option xdmp:mapping "false";

try {
  sec:remove-credential("marklogic-unit-test-credentials")
} catch (\$e) {()};

xquery version "1.0-ml";

import module namespace sec = "http://marklogic.com/xdmp/security"
      at "/MarkLogic/security.xqy";

declare option xdmp:mapping "false";

sec:create-credential(
   "marklogic-unit-test-credentials",
   "Credentials for ML Rest Helper Calls",
   "${mlUsername}",
   "${mlPassword}",
   (),
   (),
   fn:false(),
   sec:uri-credential-target("http://localhost:${mlRestPort}/.*","digest"),
   xdmp:default-permissions()
)
""";
    try {
      String result
      result = client.newServerEval().xquery(xquery).evalAs(String.class);
      if (result != null) {
        println result
      }
    } finally {
      client.release()
    }
  }
}

mlPostDeploy.dependsOn createHttpCredentials
tasks.test.dependsOn mlDeploy
